pipeline Exercise4 {
  Exercise4Extractor -> Exercise4Unarchiver;
  Exercise4Unarchiver
    -> Exercise4FilePicker
    -> Exercise4TextFileInterpreter
    -> Exercise4CSVInterpreter
    -> Exercise4CellWriter
    -> Exercise4HeaderCellWriter
    -> Exercise4CoreDataSelector
    -> Exercise4TableInterpreter
    -> Exercise4TempTransformer
    -> Exercise4BatteryTransformer
    -> Exercise4Loader;

  block Exercise4Extractor oftype HttpExtractor {
      url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
  }

  block Exercise4Unarchiver oftype ArchiveInterpreter {
      archiveType: "zip";
  }

  block Exercise4FilePicker oftype FilePicker {
      path: "/data.csv";
  }

  block Exercise4TextFileInterpreter oftype TextFileInterpreter {}

  block Exercise4CSVInterpreter oftype CSVInterpreter {
      delimiter: ';';
  }

  valuetype Positive oftype integer {
      constraints: [ positiveValue ];
  }

  constraint positiveValue oftype RangeConstraint {
      lowerBound: 0;
  }

  block Exercise4CellWriter oftype CellWriter {
      at: range A1:E1;
      write: ["id", "producer", "model", "month", "temperature"];
  }

  block Exercise4HeaderCellWriter oftype CellWriter {
      at: cell J1;
      write: ["battery_temperature"];
  }

  block Exercise4CoreDataSelector oftype CellRangeSelector {
      select: range A1:J*;
  }

  block Exercise4TableInterpreter oftype TableInterpreter {
      header: true;
      columns: [
          "id" oftype Positive,
          "producer" oftype text,
          "model" oftype text,
          "month" oftype integer,
          "temperature" oftype decimal,
          "battery_temperature" oftype decimal
      ];
  }

  transform CelsiusToFahrenheit {
      from Celsius oftype decimal;
      to Fahrenheit oftype decimal;
      Fahrenheit : (Celsius * 9/5) + 32;
  }

  block Exercise4TempTransformer oftype TableTransformer {
      inputColumns: ["temperature"];
      outputColumn: "temperature";
      use: CelsiusToFahrenheit;
  }

  block Exercise4BatteryTransformer oftype TableTransformer {
      inputColumns: ["battery_temperature"];
      outputColumn: "battery_temperature";
      use: CelsiusToFahrenheit;
  }

  block Exercise4Loader oftype SQLiteLoader {
      table: "temperatures";
      file: "./temperatures.sqlite";
  }
}